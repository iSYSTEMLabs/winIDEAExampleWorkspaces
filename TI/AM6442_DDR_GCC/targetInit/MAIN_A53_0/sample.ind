INPUT(& &)

INCLUDE ../common.ind

_estack = (ORIGIN(DDR_MAIN_A53_0) + LENGTH(DDR_MAIN_A53_0) - 16);


SECTIONS
{  
  .text_out :            /* Code goes into RAM */
  {
    KEEP(*(.A53_0_boot_vectors*)) /* Startup code */
    _vectorTable = .;
    KEEP(*(.text.resetHandler)) /* Startup code */
    *(.text*)            /* .text* sections (code) */
    _etext = .;          /* define a global symbols at end of code */
  } >DDR_MAIN_A53_0

  .rodata_out :          /* Constant data goes into RAM */
  {
    . = ALIGN(4);
    *(.rodata*)          /* .rodata* sections (constants, strings, etc.) */
    . = ALIGN(4);
    _sidata = .;         /* Values for initialized data go here */
  } >DDR_MAIN_A53_0

  .data_out :            /* Initialized data sections goes into RAM */
  {
    _sdata = .;          /* Initialized data starts here */
    *(.data*)            /* .data* sections */
    . = ALIGN(4);
    _edata = .;          /* Initialized data ends here */
  } >DDR_MAIN_A53_0 AT> DDR_MAIN_A53_0         /* Loaded in RAM */
  
  .bss_out :             /* Uninitialized data section follows in RAM */
  {
    . = ALIGN(4);
    _sbss = .;           /* Uninitialized (zero initialized) data starts here */
    *(.bss*)
    *(COMMON)
    . = ALIGN(4);
    _ebss = .;           /* Uninitialized (zero initialized) data ends here */
  } >DDR_MAIN_A53_0
  
  /* Remove information from the standard libraries */
  /DISCARD/ :
  {
    *(.note.gnu.build-id*)
    libc.a ( * )
    libm.a ( * )
    libgcc.a ( * )
  }
}