INPUT(&&)

MEMORY
{
  RAM_MAIN_P (rwx) : ORIGIN = 0x70040000, LENGTH = 128K
  RAM_MAIN_D (rwx) : ORIGIN = 0x70060000, LENGTH = 128K  
  
  ATCM_MAIN_0_R5_1  (rwx) : ORIGIN = 0x78200000, LENGTH = 64K
}


_estack_sys = (ORIGIN(RAM_MAIN_D) + LENGTH(RAM_MAIN_D) - 4) & 0xFFFFFFFC;
_estack_usr = _estack_sys - 0x800;
_estack_svc = _estack_usr - _stack_size_usr;
_estack_abt = _estack_svc - _stack_size_svc;
_estack_und = _estack_abt - _stack_size_abt;
_estack_irq = _estack_und - _stack_size_und;
_estack_fiq = _estack_irq - _stack_size_irq;

_stack_size_usr = 0x800;
_stack_size_svc = 0x800;
_stack_size_abt = 0x800;
_stack_size_und = 0x800;
_stack_size_irq = 0x800;
_stack_size_fiq = 0x800;

SECTIONS
{
  .boot :
  {
    KEEP(*(.main_0_r5_1_boot_vectors))    
  } >ATCM_MAIN_0_R5_1
  
  .text :
  {
    . = ALIGN(4);
    *(.text)
    *(.text*)

    *(.rodata)
    *(.rodata*)
    *(.glue_7) *(.glue_7t)
    *(.eh*)
    . = ALIGN(4);
  } >RAM_MAIN_P
  
  .exh :
  {
    *(.ARM.exidx*)
    . = ALIGN(4);
    _etext = .;
  } >RAM_MAIN_P
  
  
  _sidata = .;
  .data : AT ( _sidata )
  {
      _sdata = .;
      *(vtable)
      *(.data*)
      . = ALIGN(4);
      _edata = .;
  } > RAM_MAIN_D

  .bss :
  {
      _sbss = .;
      *(.bss*)
      *(COMMON)
      . = ALIGN(4);
      _ebss = .;
      __bss_end__ = _ebss;
  } > RAM_MAIN_D
  
  PROVIDE ( end = _ebss );
  PROVIDE ( _end = _ebss );
}

ROM_USAGE = 100 * (SIZEOF(.text) + SIZEOF(.data)) / LENGTH(RAM_MAIN_P);
RAM_USAGE = 100 * (SIZEOF(.data) + SIZEOF(.bss))  / LENGTH(RAM_MAIN_D);