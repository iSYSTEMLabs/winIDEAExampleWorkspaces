INPUT(&&)

MEMORY
{
  RAM_256K_CM4        (rwx) : ORIGIN = 0x05000000, LENGTH = 256K
  MSRAM_256K0_RAM     (rwx) : ORIGIN = 0x70000000, LENGTH = 256K
  MSRAM_256K1_RAM     (rwx) : ORIGIN = 0x70040000, LENGTH = 256K
  MSRAM_256K2_RAM     (rwx) : ORIGIN = 0x70080000, LENGTH = 256K
  MSRAM_256K3_RAM     (rwx) : ORIGIN = 0x700C0000, LENGTH = 256K
  MSRAM_256K4_RAM     (rwx) : ORIGIN = 0x70100000, LENGTH = 256K
  
  ATCM_MAIN_0_R5_0  (rwx) : ORIGIN = 0x78000000, LENGTH = 64K
  ATCM_MAIN_0_R5_1  (rwx) : ORIGIN = 0x78200000, LENGTH = 64K
  ATCM_MAIN_1_R5_0  (rwx) : ORIGIN = 0x78400000, LENGTH = 64K
  ATCM_MAIN_1_R5_1  (rwx) : ORIGIN = 0x78600000, LENGTH = 64K
}

_bootAddr_CM4 = ORIGIN(RAM_256K_CM4);

_estack_sys = (ORIGIN(MSRAM_256K1_RAM) + LENGTH(MSRAM_256K1_RAM) - 4) & 0xFFFFFFFC;
_estack_usr = _estack_sys - 0x800;
_estack_svc = _estack_usr - _stack_size_usr;
_estack_abt = _estack_svc - _stack_size_svc;
_estack_und = _estack_abt - _stack_size_abt;
_estack_irq = _estack_und - _stack_size_und;
_estack_fiq = _estack_irq - _stack_size_irq;

_stack_size_usr = 0x800;
_stack_size_svc = 0x800;
_stack_size_abt = 0x800;
_stack_size_und = 0x800;
_stack_size_irq = 0x800;
_stack_size_fiq = 0x800;

SECTIONS
{
  .boot :
  {
    KEEP(*(.main_0_r5_1_boot_vectors))    
  } >ATCM_MAIN_0_R5_1

  .text :
  {
    . = ALIGN(4);
    *(.text)
    *(.text*)
    *(.rodata)
    *(.rodata*)
    *(.glue_7) *(.glue_7t)
    *(.eh*)
    . = ALIGN(4);
  } >MSRAM_256K1_RAM
  
  .exh :
  {
    *(.ARM.exidx*)
    . = ALIGN(4);
    _etext = .;
  } >MSRAM_256K1_RAM
  
  _sidata = .;
  
  .data : AT ( _sidata )
  {
      _sdata = .;
      *(vtable)
      *(.data*)
      . = ALIGN(4);
      _edata = .;
  } > MSRAM_256K1_RAM

  .bss :
  {
      _sbss = .;
      *(.bss*)
      *(COMMON)
      . = ALIGN(4);
      _ebss = .;
      __bss_end__ = _ebss;
  } > MSRAM_256K1_RAM
  
  PROVIDE ( end = _ebss );
  PROVIDE ( _end = _ebss );
}

RAM_USAGE = 100 * (SIZEOF(.data) + SIZEOF(.bss))  / LENGTH(MSRAM_256K1_RAM);