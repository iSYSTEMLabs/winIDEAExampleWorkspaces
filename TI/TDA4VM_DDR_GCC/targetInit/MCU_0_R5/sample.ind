INPUT(&&)

INCLUDE ../common.ind
ENTRY(Reset_Handler)

_stack_size_usr = 0x400;
_stack_size_svc = 0x400;
_stack_size_abt = 0x400;
_stack_size_und = 0x400;
_stack_size_irq = 0x400;
_stack_size_fiq = 0x400;

_estack_sys = (ORIGIN(DDR_MCU_0_R5) + LENGTH(DDR_MCU_0_R5) - 4) & 0xFFFFFFFC;
_estack_usr = _estack_sys - 0x800;
_estack_svc = _estack_usr - _stack_size_usr;
_estack_abt = _estack_svc - _stack_size_svc;
_estack_und = _estack_abt - _stack_size_abt;
_estack_irq = _estack_und - _stack_size_und;
_estack_fiq = _estack_irq - _stack_size_irq;

SECTIONS
{
  .vectors :
  {
    *(.vector*)
    *(.resetCommonR5*)
  } > CR5_COMMON_BOOT
  
  .text :
  {
    KEEP(*(.isr_vector))
    *(.text)
    *(.text*)
    *(.rodata)
    *(.rodata*)
    *(.glue_7) *(.glue_7t)
    *(.eh*)
    . = ALIGN(4);
  } >DDR_MCU_0_R5
  
  .exh :
  {
    *(.ARM.exidx*)
    . = ALIGN(4);
    _etext = .;
  } >DDR_MCU_0_R5
  
  .data :
  {
      _sdata = .;
      *(vtable)
      *(.data*)
      . = ALIGN(4);
      _edata = .;
  } > DDR_MCU_0_R5 AT> DDR_MCU_0_R5

  .bss :
  {
      _sbss = .;
      *(.bss*)
      *(COMMON)
      . = ALIGN(4);
      _ebss = .;
      __bss_end__ = _ebss;
  } > DDR_MCU_0_R5
  
  PROVIDE ( end = _ebss );
  PROVIDE ( _end = _ebss );
}

ROM_USAGE = 100 * (SIZEOF(.text) + SIZEOF(.data)) / LENGTH(DDR_MCU_0_R5);
RAM_USAGE = 100 * (SIZEOF(.data) + SIZEOF(.bss))  / LENGTH(DDR_MCU_0_R5);



