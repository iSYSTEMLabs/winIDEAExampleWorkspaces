/* Linker file for CCFC2012BC */

MEMORY
{
    resetvector:        org = 0x00000000,   len = 0x00000008
    init:               org = 0x00000020,   len = 0x00000FE0
    irq_handlers:       org = 0x00001000,   len = 0x00001000
    internal_flash:     org = 0x00002000,   len = 0x0000E000
    internal_ram:       org = 0x40000000,   len = 0x00015000
    stack :             org = 0x40016000,   len = 0x100 /* Start location for Stack */
    heap  :             org = 0x40017000,   len = 0x100 /* Heap start location */
}

__RAM_START_ADDR = ADDR(internal_ram);
__RAM_END_ADDR = ADDR(stack) + SIZEOF(stack);
__RAM_SIZE = __RAM_END_ADDR - __RAM_START_ADDR;

FORCEACTIVE { "bam_rchw" "bam_resetvector" }
		
SECTIONS
{
    .__bam_bootarea LOAD (0x00000000): {} > resetvector

    GROUP : {
      .init LOAD (0x00000020) : {}
      .init_vle (VLECODE) LOAD (_e_init) : {
        *(.init)
        *(.init_vle)
      }
    } > init
	
    GROUP : 
    { 
      .intc_hw_branch_table  (VLECODE) LOAD (ADDR(irq_handlers))  : {}
    } > irq_handlers
    

    GROUP  : {
      .ivor_branch_table (VLECODE) ALIGN (2048)   : {}
      .text : {} 
      .text_vle (VLECODE) ALIGN(0x08): {
         *(.text)
         *(.text_vle)
       }
       .rodata (CONST) : {
         *(.rdata)
         *(.rodata)
       }
       .ctors : {}
       .dtors : {}
       extab : {}
       extabindex : {}
    } > internal_flash
    
    GROUP : {
       .__uninitialized_intc_handlertable ALIGN(2048) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
    } > internal_ram
}

/* Metrowerks CodeWarrior compiler address designations */

_stack_addr = ADDR(stack)+SIZEOF(stack);
_stack_end  = ADDR(stack);
__ROM_SDATA_SIZE    = SIZEOF(.sdata);
__ROM_SDATA_ADDR    = ROMADDR(.sdata);
__RAM_SDATA_ADDR    = ADDR(.sdata);

__IVPR_VALUE = ADDR(irq_handlers);
EXCEPTION_HANDLERS = ADDR(internal_flash);
