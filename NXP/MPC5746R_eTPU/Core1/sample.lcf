/* lcf file for MPC5516 main core (e200z1)*/

MEMORY
{
    resetvector:        org = 0x01000000,   len = 0x00000020
    init:               org = 0x01000020,   len = 0x00000FE0
    exception_handlers: org = 0x01001000,   len = 0x00001000
    internal_flash:     org = 0x01002000,   len = 0x0000E000
    internal_ram:       org = 0x40008000,   len = 0x00002000  
    heap  :             org = 0x4000A000,   len = 0x1000 /* Heap start location */
    stack :             org = 0x4000B000,   len = 0x1000 /* Start location for Stack */
}

__RAM_START_ADDR = ADDR(internal_ram);
__RAM_END_ADDR = ADDR(stack) + SIZEOF(stack);
__RAM_SIZE = __RAM_END_ADDR - __RAM_START_ADDR;

FORCEACTIVE { "RCHW" "CFG_bits0","CFG_bits1","CFG_bits2","CPU0_resetvector","CPU1_resetvector","CPU1C_resetvector"}

SECTIONS
{
    .__bam_bootarea LOAD (0x01000000): {} > resetvector

    .init  LOAD (0x01000020): {} > init
    .init_vle (VLECODE) LOAD (ADDR(.init)+SIZEOF(.init)): {
        *(.init)
        *(.init_vle)
     } > init

    .xptn_vectors (VLECODE) LOAD (0x01001000) ALIGN (0x100): {} > exception_handlers 

    GROUP  : {
      .text : {} 
      .text_vle (VLECODE) ALIGN(0x08): {
         *(.text)
         *(.text_vle)
       }
       .rodata (CONST) : {
         *(.rdata)
         *(.rodata)
       }
       .ctors : {}
       .dtors : {}
       extab : {}
       extabindex : {}
    } > internal_flash
    
    GROUP : {
       .__uninitialized_intc_handlertable ALIGN(0x10) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
    } > internal_ram
}

/* Metrowerks CodeWarrior compiler address designations */

_stack_addr = ADDR(stack)+SIZEOF(stack);
_stack_end  = ADDR(stack);
__ROM_SDATA_SIZE    = SIZEOF(.sdata);
__ROM_SDATA_ADDR    = ROMADDR(.sdata);
__RAM_SDATA_ADDR    = ADDR(.sdata);

/* Exceptions Handlers Location (used in z650Exceptions.c for IVPR initialization) */

EXCEPTION_HANDLERS = ADDR(exception_handlers);
