/* lcf file for MPC5516 */

MEMORY
{
    resetvector:        org = 0x00000000,   len = 0x00000008
    init:               org = 0x00000020,   len = 0x00000FE0
    exception_handlers: org = 0x00001000,   len = 0x00001000
    internal_flash:     org = 0x00002000,   len = 0x0000E000
    internal_flash1:    org = 0x00010000,   len = 0x00010000
    internal_ram:       org = 0x40000000,   len = 0x00008000
    stack :             org = 0x4000C000,   len = 0x2000 /* Start location for Stack */
    heap  :             org = 0x40008000,   len = 0x2000 /* Heap start location */
}

FORCEACTIVE { "bam_rchw" "bam_resetvector" }
		
SECTIONS
{
    .__bam_bootarea LOAD (0x00000000): {} > resetvector

    
    GROUP : {
      .init LOAD (0x00000020) : {}
      .init_vle (VLECODE) LOAD (_e_init) : {
        *(.init)
        *(.init_vle)
      }
    } > init

    .__exception_handlers  LOAD (0x00001000): {} > exception_handlers 
    
    GROUP  : {
        .ivor_branch_table   LOAD (ADDR(internal_flash1))   : {}
        .ivor_handlers     LOAD (_e_ivor_branch_table) : {}
    } > internal_flash1

    GROUP  : {
      .text : {} 
      .text_vle (VLECODE) ALIGN(0x08): {
         *(.interruptHandlerSection)
         *(.text_vle)
         *(.text)
       }
       .rodata (CONST) : {
         *(.rdata)
         *(.rodata)
       }
       .ctors : {}
       .dtors : {}
       extab : {}
       extabindex : {}
    } > internal_flash
    
    GROUP : {
       .__uninitialized_intc_handlertable ALIGN(2048) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
    } > internal_ram
}

/* Metrowerks CodeWarrior compiler address designations */

_stack_addr = ADDR(stack)+SIZEOF(stack);
_stack_end  = ADDR(stack);
_heap_addr  = ADDR(heap);
_heap_end   = ADDR(heap)+SIZEOF(heap);

__IVPR_VALUE = ADDR(internal_flash1);

/* Exceptions Handlers Location (used in z650Exceptions.c for IVPR initialization) */

EXCEPTION_HANDLERS = ADDR(exception_handlers);
